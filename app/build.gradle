apply plugin: 'com.android.application'

android {
    signingConfigs {
        releaseSigning {
            keyAlias 'chilloutrecords'
            keyPassword 'chilloutrecords'
            storeFile file('../chillout_records_key.jks')
            storePassword 'chilloutrecords'
        }
    }
    compileSdkVersion rootProject.compileSDK
    buildToolsVersion rootProject.buildTools

    defaultConfig {
        applicationId "com.chilloutrecords"
        minSdkVersion rootProject.minSDK
        targetSdkVersion rootProject.targetSDK
        versionCode rootProject.versionCode
        versionName "$rootProject.versionName"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        resConfigs "en"

        // RETROFIT TIMEOUT
        buildConfigField 'Integer', 'CONNECTION_TIMEOUT', '20'

        // DATABASE VERSION
        buildConfigField 'Integer', 'DATABASE_VERSION', "$rootProject.versionCode"

        // FIREBASE INIT
        resValue "string", "FB_API_KEY", FB_API_KEY
        buildConfigField 'String', 'FB_APP_ID', FB_APP_ID
        buildConfigField 'String', 'FB_STORE', FB_STORE

        // FIREBASE PATHS
        buildConfigField 'String', 'FB_DB_URL', FB_DB_URL
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            splits {
                abi {
                    enable true
                    reset()
                    include 'armeabi-v7a'
                    universalApk false
                }
            }
            signingConfig signingConfigs.releaseSigning
            applicationVariants.all { variant -> renameAPKFile(variant) }
        }

        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            splits {
                abi {
                    enable true
                    reset()
                    include 'armeabi-v7a', 'x86'
                    universalApk false
                }
            }
            signingConfig signingConfigs.releaseSigning
            applicationVariants.all { variant -> renameAPKFile(variant) }
        }
    }

    flavorDimensions "flavors"
    productFlavors {
        free {
            // APP NAME AND ICON
            applicationIdSuffix = ".free"
            resValue "string", "app_name", "Chillout Records (Free)"
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher"
            ]
            // APP IS FREE VERSION - WITH ADS
//            buildConfigField 'Boolean', 'IS_PRO', false
        }

        pro {
            // APP NAME AND ICON
            applicationIdSuffix = ".pro"
            resValue "string", "app_name", "Chillout Records (pro)"
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher"
            ]
            // APP IS PRO VERSION - AD FREE
//            buildConfigField 'Boolean', 'IS_PRO', true
        }
    }
}


def renameAPKFile(variant) {
    variant.outputs.all { output ->
        outputFileName = new File(
                "Chillout Records ("
                        + variant.buildType.name
                        + " - "
                        + variant.versionName
                        + " )"
                        + ".apk"
        )
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'

    // RETROFIT
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'

    // JSON PARSING
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // PICTURE LOADING
    implementation 'com.github.bumptech.glide:glide:4.9.0'

    // ROUNDED IMAGE VIEW
    implementation 'com.makeramen:roundedimageview:2.3.0'

    // FIREBASE DEPENDENCIES
    implementation 'com.google.firebase:firebase-auth:19.1.0'
    implementation 'com.google.firebase:firebase-messaging:20.0.0'
    implementation 'com.google.firebase:firebase-invites:17.0.0'
    implementation 'com.google.firebase:firebase-perf:19.0.1'
    implementation 'com.google.firebase:firebase-database:19.2.0'

    // GOOGLE
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.google.android.gms:play-services-analytics:17.0.0'

    // ANIMATIONS
    implementation 'com.daimajia.easing:library:2.1@aar'
    implementation 'com.daimajia.androidanimations:library:2.3@aar'

    // NOTIFICATION BADGES
    implementation "me.leolin:ShortcutBadger:1.1.22@aar"

    // GIF VIEWER
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.8'

    // VIDEO PAYER
    implementation 'com.afollestad:easyvideoplayer:0.3.0'

}

apply plugin: 'com.google.gms.google-services'