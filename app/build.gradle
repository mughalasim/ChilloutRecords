apply plugin: 'com.android.application'

android {
    signingConfigs {
        releaseSigning {
            keyAlias 'chilloutrecords'
            keyPassword 'chilloutrecords'
            storeFile file('../chillout_records_key.jks')
            storePassword 'chilloutrecords'
        }
    }
    compileSdkVersion rootProject.compileSDK
    buildToolsVersion rootProject.buildTools

    defaultConfig {
        applicationId "com.chilloutrecords"
        minSdkVersion rootProject.minSDK
        targetSdkVersion rootProject.targetSDK
        versionCode rootProject.versionCode
        versionName "$rootProject.versionName"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        resConfigs "en"

        // FIREBASE DATABASE URLS
        buildConfigField 'String', 'DB_REF_APP_VERSION', DB_REF_APP_VERSION
        buildConfigField 'String', 'DB_REF_ABOUT_US', DB_REF_ABOUT_US
        buildConfigField 'String', 'DB_REF_POLICY', DB_REF_POLICY
        buildConfigField 'String', 'DB_REF_USERS', DB_REF_USERS
        buildConfigField 'String', 'DB_REF_VIDEOS', DB_REF_VIDEOS
        buildConfigField 'String', 'DB_REF_TRACKS', DB_REF_TRACKS
        buildConfigField 'String', 'DB_REF_COLLECTIONS', DB_REF_COLLECTIONS
        buildConfigField 'String', 'DB_REF_SINGLES', DB_REF_SINGLES

        // SONG TYPES
        buildConfigField 'String', 'SONG_TYPE_SINGLE',  SONG_TYPE_SINGLE
        buildConfigField 'String', 'SONG_TYPE_MIXTAPE', SONG_TYPE_MIXTAPE
        buildConfigField 'String', 'SONG_TYPE_ALBUM',   SONG_TYPE_ALBUM

        // APP VERSION
        resValue "string", 'app_version', "Version: $rootProject.versionName"

        // FIREBASE INIT
        resValue "string", "FB_API_KEY", FB_API_KEY
        buildConfigField 'String', 'FB_STORE', FB_STORE

        // FIREBASE PATHS
        buildConfigField 'String', 'FB_DB_URL', FB_DB_URL
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            splits {
                abi {
                    enable true
                    reset()
                    include 'armeabi-v7a'
                    universalApk false
                }
            }
            signingConfig signingConfigs.releaseSigning
            applicationVariants.all { variant -> renameAPKFile(variant) }
        }

        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            splits {
                abi {
                    enable true
                    reset()
                    include 'armeabi-v7a', 'x86'
                    universalApk false
                }
            }
            signingConfig signingConfigs.releaseSigning
            applicationVariants.all { variant -> renameAPKFile(variant) }
        }
    }

    flavorDimensions "flavors"
    productFlavors {
        free {
            // APP NAME AND ICON
            applicationIdSuffix = ".free"
            resValue "string", "app_name", "Chillout Records (Free)"
            manifestPlaceholders = [
                    icon: "@mipmap/ic_launcher_free",
                    icon_round: "@mipmap/ic_launcher_free_round"
            ]
            // FIREBASE APP ID
            buildConfigField 'String', 'FB_APP_ID', FB_APP_ID_FREE
            // APP IS FREE VERSION - WITH ADS
//            buildConfigField 'Boolean', 'IS_PRO', false
        }

        pro {
            // APP NAME AND ICON
            applicationIdSuffix = ".pro"
            resValue "string", "app_name", "Chillout Records (pro)"
            manifestPlaceholders = [
                    icon: "@mipmap/ic_launcher_pro",
                    icon_round: "@mipmap/ic_launcher_free_round"
            ]
            // FIREBASE APP ID
            buildConfigField 'String', 'FB_APP_ID', FB_APP_ID_PRO
            // APP IS PRO VERSION - AD FREE
//            buildConfigField 'Boolean', 'IS_PRO', true
        }
    }
}


def renameAPKFile(variant) {
    variant.outputs.all { output ->
        outputFileName = new File(
                "Chillout Records ("
                        + variant.buildType.name
                        + " - "
                        + variant.versionName
                        + " )"
                        + ".apk"
        )
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'

    // JSON PARSING
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // PICTURE LOADING
    implementation 'com.github.bumptech.glide:glide:4.9.0'

    // ROUNDED IMAGE VIEW
    implementation 'com.makeramen:roundedimageview:2.3.0'

    // FIREBASE DEPENDENCIES
    implementation 'com.google.firebase:firebase-auth:19.1.0'
    implementation 'com.google.firebase:firebase-messaging:20.0.0'
    implementation 'com.google.firebase:firebase-invites:17.0.0'
    implementation 'com.google.firebase:firebase-perf:19.0.1'
    implementation 'com.google.firebase:firebase-database:19.2.0'

    // GOOGLE
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.google.android.gms:play-services-analytics:17.0.0'

    // ANIMATIONS
    implementation 'com.daimajia.easing:library:2.1@aar'
    implementation 'com.daimajia.androidanimations:library:2.3@aar'

    // NOTIFICATION BADGES
    implementation "me.leolin:ShortcutBadger:1.1.22@aar"

    // VIDEO PAYER
    implementation 'com.afollestad:easyvideoplayer:0.3.0'

}

apply plugin: 'com.google.gms.google-services'