apply plugin: 'com.android.application'

android {
    signingConfigs {
        releaseSigning {
            keyAlias 'chilloutrecords'
            keyPassword 'chilloutrecords'
            storeFile file('../chillout_records_key.jks')
            storePassword 'chilloutrecords'
        }
    }
    compileSdkVersion rootProject.compileSDK
    buildToolsVersion rootProject.buildTools

    defaultConfig {
        applicationId "com.chilloutrecords"
        minSdkVersion rootProject.minSDK
        targetSdkVersion rootProject.targetSDK
        versionCode rootProject.versionCode
        versionName "$rootProject.versionName"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        resConfigs "en"

        // FIREBASE DATABASE URLS
        buildConfigField 'String', 'DB_REF_APP_VERSION', DB_REF_APP_VERSION
        buildConfigField 'String', 'DB_REF_ABOUT_US', DB_REF_ABOUT_US
        buildConfigField 'String', 'DB_REF_USERS', DB_REF_USERS
        buildConfigField 'String', 'DB_REF_VIDEOS', DB_REF_VIDEOS
        buildConfigField 'String', 'DB_REF_COLLECTIONS', DB_REF_COLLECTIONS
        buildConfigField 'String', 'DB_REF_SINGLES', DB_REF_SINGLES

        // FIREBASE STORAGE URLS
        buildConfigField 'String', 'STORAGE_IMAGES', STORAGE_IMAGES
        buildConfigField 'String', 'STORAGE_VIDEOS', STORAGE_VIDEOS
        buildConfigField 'String', 'STORAGE_MUSIC',  STORAGE_MUSIC

        // SONG TYPES
        buildConfigField 'String', 'SONG_TYPE_SINGLE',  SONG_TYPE_SINGLE
        buildConfigField 'String', 'SONG_TYPE_MIXTAPE', SONG_TYPE_MIXTAPE
        buildConfigField 'String', 'SONG_TYPE_ALBUM',   SONG_TYPE_ALBUM

        // APP VERSION
//        resValue "string", 'APP_VERSION', "Version: $rootProject.versionName"

        // FIREBASE INIT
        resValue "string", "FB_API_KEY", FB_API_KEY
        buildConfigField 'String', 'FB_STORE', FB_STORE

        // FIREBASE PATHS
        buildConfigField 'String', 'FB_DB_URL', FB_DB_URL

        // DEFAULT PATHS
        buildConfigField 'String', 'DEFAULT_HOME',       DEFAULT_HOME
        buildConfigField 'String', 'DEFAULT_PROFILE_ART',DEFAULT_PROFILE_ART
        buildConfigField 'String', 'DEFAULT_TRACK_ART',  DEFAULT_TRACK_ART
        buildConfigField 'String', 'DEFAULT_VIDEO_ART',  DEFAULT_VIDEO_ART
        buildConfigField 'String', 'DEFAULT_ALBUM_ART',  DEFAULT_ALBUM_ART
        buildConfigField 'String', 'DEFAULT_SHARE_LINK', DEFAULT_SHARE_LINK

        // TEST LOGIN CREDENTIALS
        buildConfigField 'String', 'TEST_EMAIL',    TEST_EMAIL
        buildConfigField 'String', 'TEST_PASSWORD', TEST_PASSWORD

        // SIGN AND RENAME
        signingConfig signingConfigs.releaseSigning
        applicationVariants.all { variant -> renameAPKFile(variant) }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            splits {
                abi {
                    enable true
                    reset()
                    include 'armeabi-v7a'
                    universalApk false
                }
            }
            ndk {
                abiFilters 'arm64-v8a', 'armeabi-v7a'
            }

        }

        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            splits {
                abi {
                    enable false
                }
            }
        }
    }

    flavorDimensions "flavors"
    productFlavors {
        free {
            // APP NAME AND ICON
            applicationIdSuffix = ".free"
            resValue "string", "app_name", "Chillout Records"
            manifestPlaceholders = [
                    icon: "@mipmap/ic_launcher",
                    icon_round: "@mipmap/ic_launcher_round"
            ]
            // FIREBASE APP ID
            buildConfigField 'String', 'FB_APP_ID', FB_APP_ID_FREE
        }

        pro {
            // APP NAME AND ICON
            applicationIdSuffix = ".pro"
            resValue "string", "app_name", "Chillout Records (pro)"
            manifestPlaceholders = [
                    icon: "@mipmap/ic_launcher",
                    icon_round: "@mipmap/ic_launcher_round"
            ]
            // FIREBASE APP ID
            buildConfigField 'String', 'FB_APP_ID', FB_APP_ID_PRO
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


def renameAPKFile(variant) {
    variant.outputs.all { output ->
        outputFileName = new File(
                "Chillout Records ("
                        + variant.buildType.name
                        + "-"
                        + variant.versionName
                        + ")"
                        + ".apk"
        )
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'com.android.support:design:28.0.0'

    // JSON PARSING
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // PICTURE LOADING
    implementation 'com.github.bumptech.glide:glide:4.9.0'

    // ROUNDED IMAGE VIEW
    implementation 'com.makeramen:roundedimageview:2.3.0'

    // FIREBASE DEPENDENCIES
    implementation 'com.google.firebase:firebase-auth:19.2.0'
    implementation 'com.google.firebase:firebase-messaging:20.1.0'
    implementation 'com.google.firebase:firebase-database:19.2.0'
    implementation 'com.google.firebase:firebase-storage:19.1.0'

    // GOOGLE
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.google.android.gms:play-services-analytics:17.0.0'

    // ANIMATIONS
    implementation 'com.daimajia.easing:library:2.1@aar'
    implementation 'com.daimajia.androidanimations:library:2.3@aar'

    // NOTIFICATION BADGES
    implementation "me.leolin:ShortcutBadger:1.1.22@aar"

    // VIDEO PAYER
    implementation 'com.google.android.exoplayer:exoplayer:2.10.7'

    // IMAGE SCALING
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'

    // IMAGE CROPPING AND ROTATING
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'

    // PERMISSIONS CHECKER LIBRARY
    implementation 'pub.devrel:easypermissions:1.2.0'
}

apply plugin: 'com.google.gms.google-services'